package InstanceInitializer

// standard imports
import Event
import HashList
import ClosureTimers

// custom imports
import Common
import GameConfiguration
import GlobalInitialization
import Lists

/** 副本类 **/
public class Instance
    /** 触发进入副本动作的物品 **/
    int itemId
    /** 副本名称 **/
    string name
    /** 副本序号，从0开始 **/
    int index
    /** 神秘怪物的unitId列表 **/
    HashList<int> mysteriousMonsterIds
    /** 储存实际产生的神秘怪物 **/
    unit mysteriousMonster
    /** 稀有掉落的itemId列表 **/
    HashList<int> rareDropItemIds
    /** 某次刷新实际产生的稀有掉落 **/
    int rareDropItemId
    /** 奇缘NPC的unitId列表 **/
    HashList<int> fateNpcIds
    /** 某次刷新实际产生的奇缘NPC **/
    unit fateNpc
    /** 刷新副本的定时器 **/
    timer refreshTimer
    /** 是否需要刷新副本 **/
    boolean refreshFlag = true
    /** 小怪的unitId **/
    HashList<int> creepIds
    /** BOSS的unitId，不包括神秘怪物 **/
    HashList<int> bossIds
    /** 小怪武功 **/
    int creepAbilityId
    /** boss武功 **/
    int bossAbilityId
    /** 进入副本所需的历练数 **/
    int practiceRequired
    /** 进入副本所需的金钱数 **/
    int goldRequired
    /** 进入副本所需的珍稀币数 **/
    int lumberRequired
    /** 进入副本的着陆点 **/
    vec2 landPos
    /** 刷神秘怪物地点 **/
    vec2 mysteriousMonsterPos
    /** 刷奇缘NPC地点 **/
    vec2 fateNpcPos
    construct(int itemId, int index, string name, int practiceRequired, int goldRequired, int lumberRequired, vec2 landPos)
        this.itemId = itemId
        this.index = index
        this.name = name
        this.practiceRequired = practiceRequired
        this.goldRequired = goldRequired
        this.lumberRequired = lumberRequired
        this.landPos = landPos
        refreshTimer = CreateTimer()
        EventListener.add(EVENT_PLAYER_UNIT_PICKUP_ITEM, () -> initializeInstances())

    /**
    * 传送至副本并进行初始化 
    * 1. 载入神秘怪物
    * 2. 载入稀有掉落
    * 3. 载入奇缘NPC
    **/        
    function initializeInstances()
        let p = GetTriggerUnit().getOwner()
        if refreshFlag
            refreshFlag = false
            refreshTimer.start(150, null)
            doAfter(150) ->
                refreshFlag = true
            printTimedToPlayer("|cFFFFCC00开始载入动态副本：|r |cFF99FFCC" + name + "|r", 30, p)
            printTimedToPlayer("|cFFcc99ff――――――――――", 30, p)
            loadMysteriousMonster(p)
            loadRareDropItem(p)
            loadFateNpc(p)
            printTimedToPlayer("|cFFFFCC33小怪武功：|cFF33FFFF暂未制作此玩法", 30, p)
            printTimedToPlayer("|cFFFFCC33BOSS武功：|cFF33FFFF暂未制作此玩法", 30, p)
            printTimedToPlayer("|cFFFF0033副本重置倒计时：|cFF33FFFF" + refreshTimer.getRemaining().toInt().toString() + "秒", 30, p)
        else
            printTimedToPlayer("|cFFFFCC00进入动态副本：|r |cFF99FFCC" + name + "|r", 30, p)
            printTimedToPlayer("|cFFcc99ff――――――――――", 30, p)
            printTimedToPlayer("|cFFFFCC33神秘怪物：|cFF33FFFF" + mysteriousMonster.getName(), 30, p)
            printTimedToPlayer("|cFFFFCC33稀有掉落：|cFF33FFFF" + GetObjectName(rareDropItemId), 30, p)
            printTimedToPlayer("|cFFFFCC33奇缘NPC：|cFF33FFFF" + fateNpc.getName(), 30, p)
            printTimedToPlayer("|cFFFFCC33小怪武功：|cFF33FFFF暂未制作此玩法", 30, p)
            printTimedToPlayer("|cFFFFCC33BOSS武功：|cFF33FFFF暂未制作此玩法", 30, p)
            printTimedToPlayer("|cFFFF0033副本重置倒计时：|cFF33FFFF" + refreshTimer.getRemaining().toInt().toString() + "秒", 30, p)
    
    private function loadMysteriousMonster(player p)
        if percentPossiblity(20)
            printTimedToPlayer("|cFFFFCC33载入神秘怪物：|cFF33FFFF无", 30, p)
        else
            mysteriousMonster.remove()
            let unitId = mysteriousMonsterIds.get(GetRandomInt(0, mysteriousMonsterIds.size() - 1))
            mysteriousMonster = createUnit(CREEP_PLAYER, unitId, mysteriousMonsterPos, DEFAULT_FACING)
            printTimedToPlayer("|cFFFFCC33载入神秘怪物：|cFF33FFFF" + mysteriousMonster.getName(), 30, p)

    private function loadRareDropItem(player p)
        if percentPossiblity(20)
            printTimedToPlayer("|cFFFFCC33载入稀有掉落：|cFF33FFFF无", 30, p)
        else
            rareDropItemId = rareDropItemIds.get(GetRandomInt(0, rareDropItemIds.size() - 1))
            printTimedToPlayer("|cFFFFCC33载入稀有掉落：|cFF33FFFF" + GetObjectName(rareDropItemId), 30, p)
    
    private function loadFateNpc(player p)
        if percentPossiblity(50)
            printTimedToPlayer("|cFFFFCC33载入奇缘NPC：|cFF33FFFF无", 30, p)
        else
            fateNpc.remove()
            let unitId = fateNpcIds.get(GetRandomInt(0, fateNpcIds.size() - 1))
            fateNpc = createUnit(NEUTRAL_NPC_PLAYER, unitId, fateNpcPos, DEFAULT_FACING)
            printTimedToPlayer("|cFFFFCC33载入奇缘NPC：|cFF33FFFF" + fateNpc.getName(), 30, p)


init
    // TODO 设置各副本载入unitId、itemId和pos
    Instance instance0 = new Instance('I02F', 0, "马贼洞", 0, 100, 0, vec2(0, 0)) // FIXME
    instance0.mysteriousMonsterIds = Lists.newHashList('otau', 'odkt', 'uaco')
    instance0.rareDropItemIds = Lists.newHashList('I021', 'I01L', 'I01M', 1227895110)
    instance0.fateNpcIds = Lists.newHashList(1865429068)
    
    new Instance('I03V', 1, "崆峒山", 1, 300, 0, vec2(0, 0)) // FIXME
    new Instance('I04C', 2, "塞北", 2, 1000, 0, vec2(0, 0)) // FIXME
    new Instance('I04I', 3, "绝情谷", 3, 3000, 0, vec2(0, 0)) // FIXME
    new Instance('I04V', 4, "逍遥宫", 4, 6000, 0, vec2(0, 0)) // FIXME
    new Instance('I05L', 5, "黑木崖", 5, 10000, 0, vec2(0, 0)) // FIXME